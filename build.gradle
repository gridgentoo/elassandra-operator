plugins {
  id 'idea'
  id 'com.bmuschko.docker-remote-api' version '4.10.0' apply false
}

def getGitCommitHash = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

ext.gitCommitHash = getGitCommitHash()
ext.latestElassandraVersion = (new File('./docker/supportedElassandraVersions.txt') as String[])[0]
if (!ext.javaElassandraVersion) {
  // the elassandra version used to build k8s-addons (seedProvider, config loader...)
  ext.javaElassandraVersion = ext.latestElassandraVersion
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    jcenter() // or Maven central, required for Lombok dependency
  }
}

configure(subprojects.findAll { it.name.contains("docker") }) {
  apply plugin: 'com.bmuschko.docker-remote-api'
  docker {
    registryCredentials {
      url = registryUrl
      username = registryUsername
      password = registryPassword
      email = registryEmail
    }
  }
}

task dockerBuild {
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildImage'
}

task dockerPush {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushImage'
}

task dockerBuildAllVersions {
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildAllVersions'
}

task dockerPushAllVersions {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushAllVersions'
}