import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.CsvReportRenderer
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.render.TextReportRenderer

plugins {
  id 'idea'
  id 'com.bmuschko.docker-remote-api' version '4.10.0' apply false
  id 'com.github.jk1.dependency-license-report' version '1.11'
}


def getGitCommitHash = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

ext.gitCommitHash = getGitCommitHash()
ext.latestElassandraVersion = (new File('./docker/supportedElassandraVersions.txt') as String[])[0]
if (!ext.javaElassandraVersion) {
  // the elassandra version used to build k8s-addons (seedProvider, config loader...)
  ext.javaElassandraVersion = ext.latestElassandraVersion
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://plugins.gradle.org/m2/' }
    jcenter() // or Maven central, required for Lombok dependency
  }
}

configure(subprojects.findAll { it.name.contains("docker") }) {
  apply plugin: 'com.bmuschko.docker-remote-api'
  docker {
    registryCredentials {
      url = registryUrl
      username = registryUsername
      password = registryPassword
      email = registryEmail
    }
  }
}

licenseReport {
  renderers = [new CsvReportRenderer(separator:';'), new TextReportRenderer(), new InventoryHtmlReportRenderer('index.html', 'Strapkop License Report')]
  filters = [new LicenseBundleNormalizer(bundlePath: rootProject.file('license-normalizer-bundle.json'))]
  projects = [project] + project.subprojects
  excludes = [  ]
}

task dockerBuild {
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildImage'
}

task dockerPush {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushImage'
}

task dockerBuildAllVersions {
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildAllVersions'
}

task dockerPushAllVersions {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushAllVersions'
}

// see storage account -> access keys for connectionString
task uploadLicenseReport(type: com.strapdata.AzureStorageDeployTask) {
  dependsOn generateLicenseReport
  connectionString 'DefaultEndpointsProtocol=https;AccountName=strapdata;AccountKey=R83KvCF2MzZzzFXnqi6yi5MoUgS90/CcPMakbQBSJMpQJSyIBIn13QtfKQm/oC/CDplVKdgn11fEagGlcrpPIA==;EndpointSuffix=core.windows.net'
  container 'strapkop'
  fileToDeploy 'build/reports/dependency-license/index.html'
  mimeType 'text/html'
}

task uploadLicenseNotices(type: com.strapdata.AzureStorageDeployTask) {
  dependsOn generateLicenseReport
  connectionString 'DefaultEndpointsProtocol=https;AccountName=strapdata;AccountKey=R83KvCF2MzZzzFXnqi6yi5MoUgS90/CcPMakbQBSJMpQJSyIBIn13QtfKQm/oC/CDplVKdgn11fEagGlcrpPIA==;EndpointSuffix=core.windows.net'
  container 'strapkop'
  fileToDeploy 'build/reports/dependency-license/THIRD-PARTY-NOTICES.txt'
  mimeType 'text/plain'
}