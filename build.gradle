import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.CsvReportRenderer
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.render.TextReportRenderer

plugins {
  id 'idea'
  id 'com.bmuschko.docker-remote-api' version '5.2.0' apply false
  id 'com.github.jk1.dependency-license-report' version '1.11'
  id "com.gorylenko.gradle-git-properties" version "2.2.0"
  id 'maven-publish'
}

group = 'com.strapdata.strapkop'
version = '6.8.4.1'

ext {
  allowInsecureRegistries = true
  registryUrl = System.getenv("DOCKER_REGISTRY")
  registryUsername = System.getenv("DOCKER_USERNAME")
  registryPassword = System.getenv("DOCKER_PASSWORD")

  println "registryUrl=$registryUrl"
  println "registryUsername=$registryUsername"
}

configure(subprojects.findAll { it.name.contains("docker") }) {
  apply plugin: 'com.bmuschko.docker-remote-api'
  docker {
    registryCredentials {
      url = registryUrl
      username = registryUsername
      password = registryPassword
      email = registryEmail
    }
  }
}

def getGitCommitHash = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

ext.gitCommitHash = getGitCommitHash()
ext.latestElassandraVersion = (new File('./docker/supportedElassandraVersions.txt') as String[])[0]
if (!ext.javaElassandraVersion) {
  // the elassandra version used to build k8s-addons (seedProvider, config loader...)
  ext.javaElassandraVersion = ext.latestElassandraVersion
}

licenseReport {
  renderers = [new CsvReportRenderer(separator:';'), new TextReportRenderer(), new InventoryHtmlReportRenderer('index.html', 'Strapkop License Report')]
  filters = [new LicenseBundleNormalizer(bundlePath: rootProject.file('license-normalizer-bundle.json'))]
  projects = [project] + project.subprojects
  excludes = [  ]
}


task prepareDependenciesSources(type:Exec) {
  // download the source code of the thirdparty libraries under GPL & LGPL license
  commandLine './download-sources.sh', 'sources-url.csv'
}

task dockerBuild {
  dependsOn 'prepareDependenciesSources'
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildImage'
}

task dockerPush {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushImage'
}

task dockerBuildAllVersions {
  dependsOn ':java:operator:jibDockerBuild'
  dependsOn ':java:sidecar:jibDockerBuild'
  dependsOn ':docker:dockerBuildAllVersions'
}

task dockerPushAllVersions {
  dependsOn ':java:operator:jib'
  dependsOn ':java:sidecar:jib'
  dependsOn ':docker:dockerPushAllVersions'
}

// see storage account -> access keys for connectionString
task uploadLicenseReport(type: com.strapdata.AzureStorageDeployTask) {
  dependsOn generateLicenseReport
  connectionString ***REMOVED***
  container 'strapkop'
  fileToDeploy 'build/reports/dependency-license/index.html'
  mimeType 'text/html'
}

task uploadLicenseNotices(type: com.strapdata.AzureStorageDeployTask) {
  dependsOn generateLicenseReport
  connectionString ***REMOVED***
  container 'strapkop'
  fileToDeploy 'build/reports/dependency-license/THIRD-PARTY-NOTICES.txt'
  mimeType 'text/plain'
}