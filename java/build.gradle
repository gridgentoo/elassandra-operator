plugins {
  id 'base'
  id 'com.google.cloud.tools.jib' version '1.4.0' apply false
  id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
  id "com.github.johnrengelman.shadow" version "4.0.2" apply false
  id "net.ltgt.apt-idea" version "0.21" apply false
  id 'net.minecrell.licenser' version '0.4.1'
}


subprojects {
  apply plugin: 'java-library'
  apply plugin: "jacoco"
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: "net.ltgt.apt-idea"
  apply plugin: "net.minecrell.licenser"

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://nexus.repo.strapdata.com/repository/maven-releases-public" }
    maven { url "https://jcenter.bintray.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://plugins.gradle.org/m2/' }
    jcenter() // or Maven central, required for Lombok dependency
  }

  dependencyManagement {
    imports {
      mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
    }
  }

  dependencies {
    implementation group: 'io.vavr', name: 'vavr', version: '0.9.2'
    implementation "args4j:args4j:2.33"
    // see https://docs.datastax.com/en/drivers/java/3.4/com/datastax/driver/core/GuavaCompatibility.html
    implementation "com.google.guava:guava:19.0"

    //compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"

    implementation(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.7.1') {
      exclude group: 'com.google.guava', module: 'guava'
    }

    implementation "io.kubernetes:client-java:${kubernetesClientVersion}"
    implementation "javax.inject:javax.inject:1"

    runtime "ch.qos.logback:logback-classic:1.2.3"
    testCompile "org.slf4j:jul-to-slf4j:1.7.16"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
  }

  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }
  }

  jacoco {
    toolVersion = "0.8.1"
  }

  // use JUnit 5 platform
  test {
    useJUnitPlatform()
    testLogging {
      exceptionFormat = 'full'
    }
  }

  compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.encoding = 'UTF-8'
  }

  compileTestJava {
    options.compilerArgs += '-parameters'
  }

  jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    reports {
      html.enabled = true
      xml.enabled = true
      csv.enabled = false
    }
  }

  shadowJar {
    append 'reference.conf'
    classifier = null
    version = null
    zip64 = true
    mergeServiceFiles()
  }

  license {
    header = new File(rootProject.projectDir, 'java/LICENSE.header')
    ext {
      name = 'Strapdata SAS'
      email = 'support@strapdata.com'
      year = Calendar.getInstance().get(Calendar.YEAR)
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}