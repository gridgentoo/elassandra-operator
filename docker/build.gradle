// for the moment we build only one version of Elassandra
// maybe we should always rebuild all N past releases

plugins {
  id 'base'
}

// Import task types
import com.bmuschko.gradle.docker.tasks.image.*

task copyJar(type: Copy) {
  dependsOn   ':java:k8s-addons:jar'
  from        "../java/k8s-addons/build/libs/k8s-addons.jar"
  into        'build'
}

def elassandraImageName = dockerImagePrefix + "/elassandra"

// Use task types
task dockerBuildImage(type: DockerBuildImage) {
  dependsOn 'copyJar'
  inputDir = file('.')
  tags.add(elassandraImageName + ":" + elassandraVersion)
  tags.add(elassandraImageName + ":" + project.gitCommitHash)
  tags.add(elassandraImageName + ":" + "latest")

  buildArgs = [
          'base_image': elassandraParentImage + ":" + elassandraVersion,
          'cassandra_k8s_addons_jar': './build/k8s-addons.jar'
  ]
}

task dockerPushImageLatest(type: DockerPushImage) {
  dependsOn 'dockerBuildImage'
  imageName = elassandraImageName
  tag = "latest"
}

task dockerPushImageOperatorGitCommitHash(type: DockerPushImage) {
  dependsOn 'dockerBuildImage'
  imageName = elassandraImageName
  tag = project.gitCommitHash
}

task dockerPushImageElassandraVersion(type: DockerPushImage) {
  dependsOn 'dockerBuildImage'
  imageName = elassandraImageName
  tag = elassandraVersion
}

task dockerPushImage {
  dependsOn 'dockerPushImageLatest'
  dependsOn 'dockerPushImageOperatorGitCommitHash'
  dependsOn 'dockerPushImageElassandraVersion'
}